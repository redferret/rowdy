	// Change Program
	// Author: Richard DeSilvey
	
	import "core.rowdy"
	
	func main (price, amt) {
		if $isNotSet(price) or $isNotSet(amt) {
			print "Missing Arguments, the amount due and the amount given are needed", ln
			return 1
		}
		currency = $DecimalFormat("$###,##0.00")
		change = $roundVal(amt - price, 2)
		
		print
		"Amount Due: ", $format(price, currency),	ln,
		"Amount Given: ", $format(amt, currency),	ln,
		"\nChange Due: 	", $format(change, currency),	ln,
		"----------------------------",ln,
		"Hundreds:  ", $getChange(100),ln,
		"Fifties:   ", $getChange(50),ln,
		"Twenties:  ", $getChange(20),ln,
		"Tens:      ", $getChange(10),ln,
		"Fives:     ", $getChange(5),ln,
		"Ones:      ", $getChange(1),ln,
		"Quarters:  ", $getChange(0.25),ln,
		"Dimes:     ", $getChange(0.10),ln,
		"Nickels:   ", $getChange(0.05),ln,
		"Pennies:   ", $getChange(0.01),ln,ln
	}
	
	dynamic func getChange(denom) {
		if change == denom {
			change = $roundVal(change - denom, 2)
			return 1
		} else if change > denom {
			this.qty = (change / denom) as int
			change = $roundVal(change - (qty * denom), 2)
			return qty
		} else {
			return 0
		}
	}
	