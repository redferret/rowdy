// Change Program
// Author: Richard DeSilvey

import "rowdy.core"

public class Change {
	construct(amt) {
		this.amt = amt
	}
	public:
		func getChange(denom) {
			if (this.amt == denom) {
				this.amt = $roundVal(this.amt - denom, 2)
				return 1
			} else if (this.amt > denom) {
				qty = (this.amt / denom) as int
				this.amt = $roundVal(this.amt - (qty * denom), 2)
				return qty
			} else {
				return 0
			}
		}
		
	private:
		amt = 0
}

func main(price, amt) {
	if (is price?) or (is amt?) {
		print "Missing Arguments, the amount due and the amount given are needed, respectively", ln
		return 1
	}
	currency = $DecimalFormat("$###,##0.00")
	changeDue = $roundVal(amt - price, 2)
	change = new Change(changeDue)
	
	print
	"Amount Due: ", $format(price, currency),ln,
	"Amount Given: ", $format(amt, currency),ln,ln,
	"Change Due: 	", $format(changeDue, currency),ln,
	"----------------------------",ln,
	"Hundreds:  ", $change.getChange(100),ln,
	"Fifties:   ", $change.getChange(50),ln,
	"Twenties:  ", $change.getChange(20),ln,
	"Tens:      ", $change.getChange(10),ln,
	"Fives:     ", $change.getChange(5),ln,
	"Ones:      ", $change.getChange(1),ln,
	"Quarters:  ", $change.getChange(0.25),ln,
	"Dimes:     ", $change.getChange(0.10),ln,
	"Nickels:   ", $change.getChange(0.05),ln,
	"Pennies:   ", $change.getChange(0.01),ln,ln
}

