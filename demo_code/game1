
import "core.rowdy"

func main() {
	
	total = $max(18, (33 * $rand()) as int)
	
	const prompt = func(informMessage, winMessage) {
		print informMessage, total as int, ln
		if (total <= 0){
			print winMessage, ln
			return true
		} else {
			return false
		}
	}
	
	print "The number is ", total, ln
	loop game: {
		loop userInput: {
			print "Enter 1 or 2: " read input
			if (input < 1 or input > 2) {
				print "Must only enter 1 or 2", ln
			} else {
				total = total - input
				break userInput
			}
		}
		
		if $prompt("You went: Total = ", "You Win") {
			break game
		}
		
		N = total % 3
		if (N > 0) {
			total = total - N
		} else {
			total = total - 1
		}
		
		if $prompt("Computer went: Total = ", "I Win") {
			break game
		}
	}
}