
import "core.rowdy"

const informMessageComputer = "Computer went: Total = "
const winMessageComputer = "I win"
const informMessageHuman = "You went: Total = "
const winMessageHuman = "You win"

func main() {
	
	total = $max(28, (50 * $rand()) as int)
	firstToPlay = (2 * $rand()) as int
	print "The number is ", total, ln
	loop game: {
		if firstToPlay == 0 {
			if $humanPlay() or $computerPlay() {
				break
			}
		} else {
			if $computerPlay() or $humanPlay() {
				break
			}
		}
	}
}

dynamic func humanPlay() {
	print "Enter 1 or 2: "
	loop user: {
		read input
		if (input < 1 or input > 2) {
			print "Must only enter 1 or 2", ln
		} else {
			total = total - input
			break user
		}
	}
	
	return $promptAndCheckForWin(informMessageHuman, winMessageHuman)
}

dynamic func computerPlay() {
	N = total % 3
	if (N > 0) {
		total = total - N
	} else {
		total = total - 1
	}
	
	return $promptAndCheckForWin(informMessageComputer, winMessageComputer)
}

dynamic func promptAndCheckForWin(informMessage, winMessage) {
		print informMessage, total as int, ln
		if (total <= 0){
			print winMessage, ln
			return true
		} else {
			return false
		}
	}