
import "core.rowdy"

const informMessageComputer = "Computer went: Total = "
const winMessageComputer = "I win"
const informMessageHuman = "You went: Total = "
const winMessageHuman = "You win"

func main() {
	
	total = $max(28, (50 * $rand()) as int)
	firstToPlay = (2 * $rand()) as int
	$println(concat "The number is ", total)
	loop game: {
		if firstToPlay == 0 {
			$humanPlay()
			$computerPlay()
		} else {
			$computerPlay()
			$humanPlay()
		}
	}
}

dynamic func humanPlay() {
	print "Enter 1 or 2: "
	loop user: {
		read input
		if (input < 1 or input > 2) {
			$println("Must only enter 1 or 2")
		} else {
			total = total - input
			break user
		}
	}
	
	if $promptAndCheckForWin(informMessageHuman, winMessageHuman) {
		break game
	}
}

dynamic func computerPlay() {
	N = total % 3
	if (N > 0) {
		total = total - N
	} else {
		total = total - 1
	}
	
	if $promptAndCheckForWin(informMessageComputer, winMessageComputer) {
		break game
	}
}

dynamic func promptAndCheckForWin(informMessage, winMessage) {
		$println(concat informMessage, total as int)
		if (total <= 0){
			$println(winMessage)
			return true
		} else {
			return false
		}
	}